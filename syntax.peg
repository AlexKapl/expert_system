@@grammar::EXPERT
@@eol_comments :: /[\t\r ]*#.*$/

all =
    {line_rule}*
    {line_init}*
    {line_query}*
    $;

line_rule =  {eol}*   @:rule eol;
line_init =  {eol}*ws @:init eol;
line_query = {eol}*ws @:query {eol}*;

eol = '\n';

# ---------------------
init  = '='@:{fact}*;
query = '?'@:{fact}+;

# ---------------------
rule = expr '=>' expr;

# ---------------------
expr = Xor | oterm;
Xor  = oterm {xorJoin}+;
xorJoin = '^' @:oterm;

# ---------------------
oterm = Or | aterm;
Or  = aterm {orJoin}+;
orJoin = '|' @:aterm;

# ---------------------
aterm = And | term;
And = term {andJoin}+;
andJoin = '+' @:term;

# ---------------------
term = Negation | basic;
Negation = ws'!' @:basic;

# ---------------------
basic = ws'(' ~ @:expr ')'ws | fact;
fact = ws @:var ws;
var = /[A-Z]/;

# ---------------------
ws = /[\t\r ]*/;
